import com.thomashorta.test.newsapisample.gradleconfig.*

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

// Arquivo com as configurações de signing das apps.
def newsApiPropsFile = rootProject.file("newsapi.properties")
def newsApiProps = new Properties()
newsApiProps.load(new FileInputStream(newsApiPropsFile))

android {
    compileSdkVersion Versions.compileSdkVersion
    buildToolsVersion Versions.buildToolsVersion

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
        freeCompilerArgs += "-Xopt-in=kotlin.contracts.ExperimentalContracts"
        freeCompilerArgs += "-Xopt-in=kotlinx.coroutines.ExperimentalCoroutinesApi"
    }

    defaultConfig {
        applicationId "com.thomas.test.newsapisample"
        minSdkVersion Versions.minSdkVersion
        targetSdkVersion Versions.targetSdkVersion
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField("String", "NEWS_API_KEY", newsApiProps["NEWS_API_KEY"])
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        test {
            java.srcDirs += "src/commonTest/java"
        }
        androidTest {
            java.srcDirs += "src/commonTest/java"
        }
    }

    testOptions {
        animationsDisabled = true
        unitTests.returnDefaultValues = true
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation Libs.Core.kotlin_stdlib_jdk7
    implementation Libs.Core.appcompat
    implementation Libs.Core.core_ktx
    implementation Libs.Core.coroutines_core
    implementation Libs.Core.coroutines_android
    implementation Libs.Core.legacy
    implementation Libs.Core.lifecycle_extensions
    implementation Libs.Core.lifecycle_viewmodel

    implementation Libs.DI.koin_android

    implementation Libs.Data.gson
    implementation Libs.Data.retrofit
    implementation Libs.Data.retrofit_converter_gson
    implementation Libs.Data.logging_interceptor
    implementation Libs.Data.okhttp
    implementation Libs.Data.result
    implementation Libs.Data.result_coroutines

    implementation Libs.UI.constraintlayout
    implementation Libs.UI.recyclerView
    implementation Libs.UI.material
    implementation Libs.UI.coil

    debugImplementation Libs.Test.fragment

    testImplementation Libs.Test.junit
    testImplementation Libs.Test.mockk
    testImplementation Libs.Test.core_testing
    testImplementation Libs.Test.okhttp_mockwebserver
    testImplementation Libs.Test.coroutines_test
    testImplementation Libs.Test.truth

    androidTestImplementation Libs.Test.test_runner
    androidTestImplementation Libs.Test.test_rules
    androidTestImplementation Libs.Test.junit_androidx_ext
    androidTestImplementation Libs.Test.espresso_core
    androidTestImplementation Libs.Test.espresso_contrib
    androidTestImplementation Libs.Test.truth
    androidTestImplementation Libs.Test.mockkAndroid
}
